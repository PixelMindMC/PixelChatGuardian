/*
 * This file is part of PixelChat Guardian.
 * Copyright (C) 2025 PixelMindMC
 */

// Gradle plugins
plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta17'
}

// Project metadata
group = 'de.pixelmindmc'
version = '1.1.1'

// Declare project repositories
repositories {
    // Use Maven Central repository
    mavenCentral()

    // Add SpigotMC repository for Spigot API dependencies
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    // Add Sonatype repository for other dependencies
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

// Declare project dependencies
dependencies {
    // Use compileOnly for dependencies that are provided at runtime (but not bundled)
    compileOnly 'org.spigotmc:spigot-api:1.21.6-R0.1-SNAPSHOT'
    compileOnly 'org.apache.commons:commons-lang3:3.17.0'
    compileOnly 'de.hexaoxi:carbonchat-api:3.0.0-beta.32'

    // Use implementation for dependencies that should be included in the final JAR
    implementation 'org.bstats:bstats-bukkit:3.1.0'
}

// Ensure Java 21 compatibility
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

// Configure Java compilation settings
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Set character encoding to UTF-8
}

// Configure the shadow JAR task
tasks.named("shadowJar").configure {
    archiveClassifier.set('') // Remove default classifier to ensure correct JAR naming

    // Relocate bStats package to avoid conflicts with other plugins using the same library
    relocate('org.bstats', 'de.pixelmindmc.pixelchat.metrics')

    // Exclude unnecessary META-INF files to reduce JAR size and avoid security warnings
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'

    // Define manifest attributes (metadata inside the JAR)
    manifest {
        attributes('Implementation-Title': project.name, // Plugin name
                'Implementation-Version': project.version, // Plugin version
                'Main-Class': 'de.pixelmindmc.pixelchat' // Entry point (not needed for Bukkit plugins, but useful)
        )
    }
}

// Configure the processResources task
tasks.processResources {
    filteringCharset = 'UTF-8'

    expand(version: project.version)
}

// Ensure that the shadow JAR is always built when running the build task
tasks.build {
    dependsOn tasks.shadowJar
    dependsOn tasks.processResources
}